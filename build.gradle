apply plugin: "java"

version = "1.0"
description = "Happy Birthday Greetings Kata from XPUG Italia (Matteo Vaccari)"

sourceCompatibility = 1.8


task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    check.dependsOn integrationTest
    //integrationTest.mustRunAfter test
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

/* https://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing/ */

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

dependencies {
    compile "org.apache.commons:commons-csv:1.4"
    compile "javax.mail:javax.mail-api:1.5.6"
    compile 'com.sun.mail:javax.mail:1.5.6'

    compile "javax.activation:activation:1.1.1"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.1.0"
    testCompile "org.mockito:mockito-all:1.10.19"

    integrationTestCompile ("dumbster:dumbster:1.6") {
        exclude module: 'mail'
    }

    integrationTestCompile 'info.cukes:cucumber-java:1.2.5'
    integrationTestCompile 'info.cukes:cucumber-junit:1.2.5'

}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
        }
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}
